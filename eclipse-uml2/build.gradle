/* 
 * The Maven coordinates for the project artifact
 */
ext.title = "Eclipse UML2 Vocabularies"
description='The Eclipse UML2 vocabular'

apply from: "${rootDir}/gradle/maven-deployment.gradle"

/*
 * A task to extract and merge the OML dependencies
 */
task downloadDependencies(type:io.opencaesar.oml.merge.OmlMergeTask) {
    inputZipPaths = configurations.oml.files
    outputCatalogFolder = file('build/oml')
}

/*
 * A task to download the Uml2 Ecore models
 */
apply plugin: 'de.undercouch.download'

task downloadEcore() {
    doLast {
        def libsDir = file('build/libs')
        download { 
        	src uml2_uml 
        	dest new File(libsDir, 'org.eclipse.uml2.uml.jar')
        	overwrite false
        }
        download { 
        	src uml2_types 
        	dest new File(libsDir, 'org.eclipse.uml2.types.jar')
        	overwrite false
        }
        libsDir.listFiles().each { file ->
    		copy { 
    			from zipTree(file)
    			include 'model/UML.ecore'
    			include 'model/Types.ecore'
    			into 'build/ecore'
    			eachFile { path = name } 
        		includeEmptyDirs = false
        		filteringCharset = 'UTF-8'
        		filter { line -> line.replaceAll('platform:/plugin/org.eclipse.uml2.types/model/', '') }
        		filter { line -> line.replaceAll('&#xA;&lt;p>.*&lt;/p>', '') }
    		}
    	}
    }
}

/*
 * A task to convert Ecore models to OML
 */
task ecoreToOml(type:io.opencaesar.ecore2oml.Ecore2OmlTask, dependsOn: [downloadEcore, downloadDependencies]) {
	// Ecore folder
	inputFolderPath = file('build/ecore')
	// OML Catalog
	outputCatalogPath = file('catalog.xml')
	// Options file
	optionsFilePath = file('src/options/uml.json')
}

/*
 * A task to generate Bikeshed specification for the OML catalog
 */
task omlToBikeshed(type: io.opencaesar.oml.bikeshed.Oml2BikeshedTask, dependsOn: ecoreToOml) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OWL folder
    outputFolderPath = file('build/bikeshed')
    // Publish URL
    publishUrl = 'https://opencaesar.github.io/eclipse-vocabularies/uml2'
}

/*
 * A task to generate the Bikeshed documentations in HTML
 */
task generateDocs(dependsOn: omlToBikeshed) {
    doLast {
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            exec { commandLine 'build/bikeshed/publish.bat' }
        } else {
            exec { commandLine 'chmod', '+x', 'build/bikeshed/publish.sh' }
            exec { commandLine 'build/bikeshed/publish.sh' }
        }
    }
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task omlToOwl(type:io.opencaesar.oml2owl.Oml2OwlTask, dependsOn: ecoreToOml) {
	// OML catalog
	inputCatalogPath = file('catalog.xml')
	// OWL catalog
	outputCatalogPath = file('build/owl/catalog.xml')
}

/*
 * A task to run the Openllet reasoner on the OWL catalog
 */
task owlReason(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
	// OWL catalog
	catalogPath = file('build/owl/catalog.xml')
	// Input ontology IRI to reason on
	inputOntologyIri = 'http://www.eclipse.org/uml2/5.0.0/UML-Bundle'
	// Entailment statements to generate and the ontologies to persist them in
	specs = [
		'http://www.eclipse.org/uml2/5.0.0/UML-Bundle/classes = ALL_SUBCLASS',
		'http://www.eclipse.org/uml2/5.0.0/UML-Bundle/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
		'http://www.eclipse.org/uml2/5.0.0/UML-Bundle/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
	]
	// Junit error report
	reportPath = file('build/reports/reasoning.xml')
}

/*
 * Build the project
 */
tasks.named('build') {
	dependsOn owlReason
}

/*
 * Publish artifacts
 */
task omlZip(type: Zip) {
	from file('build/oml')
	from file('src/oml')
	include "www.eclipse.org/**/*.oml"
	destinationDirectory = file('build/libs')
	archiveBaseName = project.name
	archiveVersion = project.version
    includeEmptyDirs = false
}

publishing.publications.maven.artifact omlZip

/*
 * Integration with the Eclipse IDE
 */ 
apply plugin: 'eclipse'

eclipse {
    synchronizationTasks ecoreToOml
}